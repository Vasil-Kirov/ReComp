module os

assert :: fn(cond: bool, reason: string) {
	if !cond {
		header := "Assert Failed!\n";
		print(header.data, @u32 header.count);
		print(reason.data, @u32 reason.count);
		abort();
	}
}

#if Windows {
    STD_INPUT_HANDLE  :: @u32 -10;
    STD_OUTPUT_HANDLE :: @u32 -11;
    STD_ERROR_HANDLE  :: @u32 -12;
    INVALID_HANDLE_VALUE :: @?* @u64 -1;

    CP_UTF7 :: @u32 65000;      // UTF-7 translation
    CP_UTF8 :: @u32 65001;      // UTF-8 translation

    MEM_COMMIT  :: @u32 0x00001000;
    MEM_RESERVE :: @u32 0x00002000; 
    MEM_RELEASE :: @u32 0x00008000;


    PAGE_NOACCESS          :: @u32 0x001;
    PAGE_READONLY          :: @u32 0x002;
    PAGE_READWRITE         :: @u32 0x004;
    PAGE_WRITECOPY         :: @u32 0x008;
    PAGE_EXECUTE           :: @u32 0x010;
    PAGE_EXECUTE_READ      :: @u32 0x020;
    PAGE_EXECUTE_READWRITE :: @u32 0x040;
    PAGE_EXECUTE_WRITECOPY :: @u32 0x080;
    PAGE_GUARD             :: @u32 0x100;
    PAGE_NOCACHE           :: @u32 0x200;
    PAGE_WRITECOMBINE      :: @u32 0x400;

    struct LARGE_INTEGER_STRUCT {
        LowPart: u32,
        HighPart: i32,
    }

    union LARGE_INTEGER {
        u: LARGE_INTEGER_STRUCT,
        QuadPart: i64,
    }

    CreateFileA :: fn #foreign(name: *u8, access: u32, shared: u32, security_attributes: ?*,
        creation_disposition: u32, flags_and_attribs: u32, template: ?*) -> ?*;
    WriteFile :: fn #foreign(hFile: *, lpBuff: *, nBytesToWrite: u32, lpBytesWritten: *u32, lpOverlapped: ?*) -> bool;
    GetStdHandle :: fn #foreign(Handle: u32) -> ?*;
    ExitProcess :: fn #foreign(ExitCode: u32);
    SetConsoleOutputCP :: fn #foreign(code_page: u32) -> i32; // win32 bool
    VirtualAlloc :: fn #foreign(addr: ?*, size: uint, type: u32, protect: u32) -> ?*;
    VirtualFree :: fn #foreign(addr: ?*, size: uint, type: u32) -> bool;
    ReadConsoleA :: fn #foreign(hConsoleInput: *, lpBuffer: *, nNumberOfCharsToRead: u32, lpNumberOfCharsRead: *u32, pInputControl: ?*) -> bool;
    GetFileSizeEx :: fn #foreign(file: *, out: *LARGE_INTEGER) -> bool;
    ReadFile :: fn #foreign(file: *, buf: *, bytes_to_read: u32, bytes_read: ?*u32, overlapped: ?*) -> bool;
    CloseHandle :: fn #foreign(handle: *) -> bool;

    abort :: fn() {
        ExitProcess(1);
        return;
    }

    print :: fn(data: *u8, count: u32) {
        SetConsoleOutputCP(CP_UTF8);
        handle := GetStdHandle(STD_OUTPUT_HANDLE);
        written: u32;
        if (handle != null) && (handle != INVALID_HANDLE_VALUE) {
            WriteFile(?handle, data, count, &written, null);
        }
    }

	alloc :: fn(size: int) -> ?* {
        return VirtualAlloc(null, @uint size, MEM_RESERVE | MEM_COMMIT, PAGE_READWRITE);
    }

	free :: fn(ptr: *) {
        VirtualFree(ptr, 0, MEM_RELEASE);
    }

    GENERIC_READ   :: @u32 0x80000000;
    GENERIC_WRITE  :: @u32 0x40000000;
    CREATE_NEW          :: @u32 1;
    CREATE_ALWAYS       :: @u32 2;
    OPEN_EXISTING       :: @u32 3;
    OPEN_ALWAYS         :: @u32 4;
    TRUNCATE_EXISTING   :: @u32 5;
    FILE_ATTRIBUTE_READONLY             :: @u32 0x00000001;
    FILE_ATTRIBUTE_HIDDEN               :: @u32 0x00000002;
    FILE_ATTRIBUTE_SYSTEM               :: @u32 0x00000004;
    FILE_ATTRIBUTE_DIRECTORY            :: @u32 0x00000010;
    FILE_ATTRIBUTE_ARCHIVE              :: @u32 0x00000020;
    FILE_ATTRIBUTE_DEVICE               :: @u32 0x00000040;
    FILE_ATTRIBUTE_NORMAL               :: @u32 0x00000080;
    FILE_ATTRIBUTE_TEMPORARY            :: @u32 0x00000100;
    FILE_ATTRIBUTE_SPARSE_FILE          :: @u32 0x00000200;
    FILE_ATTRIBUTE_REPARSE_POINT        :: @u32 0x00000400;
    FILE_ATTRIBUTE_COMPRESSED           :: @u32 0x00000800;
    FILE_ATTRIBUTE_OFFLINE              :: @u32 0x00001000;
    FILE_ATTRIBUTE_NOT_CONTENT_INDEXED  :: @u32 0x00002000;
    FILE_ATTRIBUTE_ENCRYPTED            :: @u32 0x00004000;
    FILE_ATTRIBUTE_INTEGRITY_STREAM     :: @u32 0x00008000;
    FILE_ATTRIBUTE_VIRTUAL              :: @u32 0x00010000;
    FILE_ATTRIBUTE_NO_SCRUB_DATA        :: @u32 0x00020000;
    FILE_ATTRIBUTE_EA                   :: @u32 0x00040000;
    FILE_ATTRIBUTE_PINNED               :: @u32 0x00080000;
    FILE_ATTRIBUTE_UNPINNED             :: @u32 0x00100000;
    FILE_ATTRIBUTE_RECALL_ON_OPEN       :: @u32 0x00040000;
    FILE_ATTRIBUTE_RECALL_ON_DATA_ACCESS :: @u32 0x00400000;

	stdin :: fn() -> * {
        h := GetStdHandle(STD_INPUT_HANDLE);
        #assert h;
        return ?h;
	}

	stdout :: fn() -> * {
        h := GetStdHandle(STD_OUTPUT_HANDLE);
        #assert h;
        return ?h;
	}

	open :: fn(name: string) -> ?* {
        h := CreateFileA(name.data, GENERIC_READ | GENERIC_WRITE, 0, null, OPEN_EXISTING, FILE_ATTRIBUTE_NORMAL, null);

        if h == null || h == INVALID_HANDLE_VALUE return null;
        return h;
    }

	write :: fn(file: *, buf: *u8, count: int) -> int {
        // @TODO: 64 bit file writes
        written: u32;
        if !WriteFile(file, buf, @u32 count, &written, null) return 0;
        return @int written;
	}
    
	read :: fn(file: *, buf: *, size: int) -> int {
        read: u32;
        // @TODO: 64 bit file reads
        if !ReadFile(file, buf, @u32 size, &read, null) return 0;
        return @int read;
        
    }

    close :: fn(handle: *) {
        CloseHandle(handle);
    }

	get_file_size :: fn(handle: *) -> int {
        out: LARGE_INTEGER;
        if GetFileSizeEx(handle, &out) == false return 0;
		return @int out.QuadPart;
	}

    struct CONSOLE_READCONSOLE_CONTROL {
        nLength: u32,
        nInitialChars: u32,
        dwCtrlWakeupMask: u32,
        dwControlKeyState: u32,
    }

    read_stdin_line :: fn(buffer: *, size: u32) -> u32 {
        handle := GetStdHandle(STD_INPUT_HANDLE);
        control := CONSOLE_READCONSOLE_CONTROL {
            dwCtrlWakeupMask = (1 << '\n'),
        };

        if handle == null {
            return 0;
        }

        read := @u32 0;
        if ReadConsoleA(?handle, buffer, size, &read, &control) == false {
            return 0;
        }

        as_chars := @*u8 buffer;

        // remove /r/n
        as_chars[read-2] = 0;
        return read - 2;
    }
}
#elif Unix {

#private
		handle_stdin  :: 0;
        handle_stdout :: 1;
        handle_stderr :: 2;

#public
#if x64 {
	SYS_read  	:: 0;
	SYS_write  	:: 1;
	SYS_open   	:: 2;
	SYS_close	:: 3;
	SYS_mmap   	:: 9;
	SYS_munmap 	:: 11;
	SYS_exit	:: 60;
	SYS_fstat	:: 5;
    SYS_clock_gettime :: 228;
	struct stat {
		pad0: [48]u8,
		st_size: int,
		pad1: [88]u8,
	}
}
#elif x86 {
	SYS_read      :: 3;
    SYS_write     :: 4;
    SYS_open      :: 5;
    SYS_close     :: 6;
    SYS_mmap      :: 90;
    SYS_munmap    :: 91;
    SYS_exit      :: 1;
    SYS_clock_gettime :: 228;
}
#elif arm32 {
    SYS_read    :: 3;
    SYS_write   :: 4;
    SYS_open    :: 5;
	SYS_close	:: 6;
    SYS_mmap    :: 90;
    SYS_munmap  :: 91;
    SYS_exit    :: 1;
    SYS_clock_gettime :: 263;
}
#elif arm64 {
    SYS_read    :: 63;
    SYS_write   :: 64;
    SYS_open    :: 56;
	SYS_close	:: 57;
    SYS_mmap    :: 222;
    SYS_munmap  :: 215;
    SYS_exit    :: 93;
    SYS_clock_gettime :: 113;
    SYS_fstat   :: 80;
}
    CLOCK_MONOTONIC :: 1;

    struct __old_kernel_stat {
        st_dev  : u16,
        st_ino  : u16,
        st_mode : u16,
        st_nlink: u16,
        st_uid  : u16,
        st_gid  : u16,
        st_rdev : u16,
        st_size : u64,
        st_atime: u64,
        st_mtime: u64,
        st_ctime: u64,
    }

    struct timespec {
        tv_sec:     int,       //* seconds */
        tv_nsec:    int,       //* nanoseconds */
    };

	syscall :: fn #foreign(number: int, args: ...) -> int;
	print :: fn(data: *u8, count: u32) {
		syscall(SYS_write, 1, data, count);
	}
	
	struct AllocCtx {
		size: int
	}

	PROT_READ  :: 0x1;
	PROT_WRITE :: 0x2;
	PROT_EXEC  :: 0x4;
	PROT_NONE  :: 0x0;

	MAP_SHARED		:: 0x00001;
	MAP_PRIVATE		:: 0x00002;
	MAP_ANONYMOUS	:: 0x00020;
	MAP_ANON		:: 0x00020;
	MAP_NORESERVE	:: 0x04000;

	O_RDONLY 	:: 0x0;
	O_WRONLY 	:: 0x1;
	O_RDWR 		:: 0x2;
	O_CREAT		:: 0x40;
	O_EXCL		:: 0x80;
	O_NOCTTY	:: 0x100;
	O_TRUNC		:: 0x200;
	O_APPEND 	:: 0x400;
	O_NONBLOCK	:: 0x800;

#if LIBC {
	exit :: fn #foreign(code: int);
	
	abort :: fn() {
		exit(1);
	}
}
#else {
	abort :: fn() {
		syscall(SYS_exit, 1);
	}
}

	alloc :: fn(size: int) -> ?* {
		myb_ctx := @?*AllocCtx syscall(SYS_mmap,
		null,						// Address
		size + size_of AllocCtx,	// Size
		PROT_READ | PROT_WRITE,		// Prot
		MAP_PRIVATE | MAP_ANON | MAP_NORESERVE,	// Flags
		-1,							// FD
		0);							// Offset
        FAILED :: -1;
        if myb_ctx == @?*void FAILED || myb_ctx == null {
            return null;
        }

        ctx := ?myb_ctx;
		
		ctx.size = size + size_of AllocCtx;
		return ctx + 1;
	}

	stdin :: fn() -> * {
		return @*void handle_stdin;
	}

	stdout :: fn() -> * {
		return @*void handle_stdout;
	}

    // @TODO: other platforms
#if x64 {
	get_file_size :: fn(fd: *) -> int {
		fstat := stat {};
		syscall(SYS_fstat, fd, &fstat);
		return fstat.st_size;
	}
}
#elif arm64 {
    get_file_size :: fn(fd: *) -> int {
        fstat := __old_kernel_stat {};
        syscall(SYS_fstat, fd, &fstat);
        return cast(int, fstat.st_size);
    }
}

	open :: fn(name: string) -> ?* {
		fd := syscall(SYS_open, name.data, O_RDWR | O_APPEND);
		if fd == -1 return null;
		return @*void fd;
	}

	write :: fn(fd: *, buf: *u8, count: int) -> int {
        written := syscall(SYS_write, fd, buf, count);
        if written == -1 return 0;
        return written;
	}

	close :: fn(fd: *) {
		syscall(SYS_close, fd);
	}

	free :: fn(ptr: *) {
		ctx := (@*AllocCtx ptr) - 1;
		syscall(SYS_munmap, ctx, ctx.size);
	}

    read_stdin_line :: fn(buffer: *, size: u32) -> u32 {
		buf := @*u8 buffer;
		count := @u32 0;
		for count < size {
			c := @u8 0;
			syscall(SYS_read, stdin(), &c, 1);
			if c == '\n' {
				break;
			}
			buf[count] = c;
			count += 1;
		}
		return count;
	}

	read :: fn(fd: *, buf: *, size: int) -> int {
		return syscall(SYS_read, fd, buf, size);
	}
}

