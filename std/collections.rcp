module collection;


#import array;
#import mem;


struct Stack<T>
{
	data: []T,
	top: int,
}


make_stack :: fn(t: type, alloc: *mem.Allocator) -> Stack<t> {
	return Stack<t> {
		data = array.create(t, alloc),
		top = 0,
	};
}

push :: fn(s: *Stack<$t>, elem: t) {
	using s;
	if top == data.count {
		array.add(&data, elem);
		top += 1;
	}
	else {
		data[top] = elem;
		top += 1;
	}
}

pop :: fn(s: *Stack<$t>) -> t {
	using s;
	top -= 1;
	result := data[top];
	return result;
}

free_stack :: fn(s: *Stack<$t>) {
	array.free(s.data);
}


struct Queue<T>
{
	data: []T,
	front: int,
	size: int,
	capacity: int,
};

make_queue :: fn(t: type, capacity: int, alloc: *mem.Allocator) -> Queue<t> {
	return Queue<t> {
		data = mem.make_slice(t, capacity, alloc),
		front = 0,
		size = 0,
		capacity = capacity,
	};
}

enqueue :: fn(q: *Queue<$t>, elem: t) {
	using q;
	data[(front + size) % capacity] = elem;
	size += 1;
}

dequeue :: fn(q: *Queue<$t>) -> t {
	using q;
 	#assert size > 0;
	result := data[front];
	front = (front + 1) % capacity;
	size -= 1;
	return result;
}




