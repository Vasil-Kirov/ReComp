mem
#import os

kb :: fn(b: int) -> int { return b << 10; }
mb :: fn(b: int) -> int { return b << 20; }
gb :: fn(b: int) -> int { return b << 30; }

// scratch allocator to be used when you don't care about storing the memory long term
// it's the user's job to reset it
temp_alloc := create_scratch(mb(1));

struct Allocator {
    alloc: fn(size: int, this: *Allocator) -> ?*,
    free:  fn(ptr: *, this: *Allocator),
    data: ?*,
}

make_slice :: fn (T: type, size: int, alloc: *Allocator) -> []T {
    res := []T{};
    res.data  = ?alloc.alloc(size * size_of T, alloc);
    res.count = size;
    return res;
}

make_type :: fn (T: type, alloc: *Allocator) -> ?*T {
    return @?*T alloc.alloc(size_of T, alloc);
}

destroy_slice :: fn(slice: *[]$T, alloc: *Allocator) {
	alloc.free(slice.data);
	slice.count = 0;
}

generic_alloc :: fn(size: int, this: *Allocator) -> ?* {
    ptr := os.alloc(size);
	if ptr != null
		set(?ptr, 0, size);
    
	return ptr;
}

generic_free :: fn(ptr: *, this: *Allocator) {
    os.free(ptr);
}

create_generic_allocator :: fn() -> Allocator {
    r := Allocator {
        alloc = generic_alloc,
        free = generic_free,
        data = null,
    };
    return r;
}

struct ScratchContext {
	start: *u8,
    current: *u8,
    max: *u8,
}

arena_alloc :: fn(size: int, this: *Allocator) -> ?* {
    ctx := @*ScratchContext this.data;
    os.assert(ctx != null, "Invalid allocator for arena_alloc()");
    os.assert(ctx.max > ctx.current, "Calling arena_alloc() with an out of memory arena");

    res := ctx.current;
    ctx.current = ctx.current + size;

	set(res, 0, size);
    
    return res;
}

no_free :: fn(p: *, this: *Allocator) {

}

create_scratch :: fn(max_size: int) -> Allocator {
    ctx := @*ScratchContext os.alloc(max_size + size_of ScratchContext);
	ctx.start   = @*u8 (ctx + 1);
    ctx.current = @*u8 (ctx + 1);
    ctx.max = ctx.current + max_size;

    r := Allocator {
        alloc = arena_alloc,
        free = no_free,
        data = ctx,
    };

    return r;
}

reset_scratch :: fn(a: *Allocator) {
    ctx := @*ScratchContext a.data;
	#assert ctx;
	ctx.current = ctx.start;
}

destroy_scratch :: fn(a: *Allocator) {
    ctx := @*ScratchContext a.data;
	#assert ctx;
    os.free(ctx);
}

copy  :: fn #link="memcpy"(dst: *, src: *, size: int) -> *;
set   :: fn #link="memset"(dst: *, c: i32, size: int) -> *;
cmp   :: fn #link="memcmp"(p1: *, p2: *, size: int) -> i32;

